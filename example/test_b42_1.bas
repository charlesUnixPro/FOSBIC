*  The accuracy of the control unity matrix P differs from book
  5 REM MATRIZENINVERISON
 10 DIM D(10,10),E(10,10),P(10,10)
 20 READ N1
 30 DATA 4
 35 MAT LET D=ZER(N1,N1)
 40 MAT READ D
 45 PRINT 'EINGABEMATRIX'
 50 MAT PRINT D
 60 MAT LET E=IDN(N1,N1)
 70 REM BEGINN DER INVERSION
 80 FOR M1=1 TO N1
 90   LET T=D(M1,M1)
100   IF T=0 GOTO 360
105   REM BEGINN DES ERSTEN EINZELSCHRITTS
110   FOR N=1 TO N1
120     LET E(M1,N)=E(M1,N)/T
130     LET D(M1,N)=D(M1,N)/T
140   NEXT N
150   LET M=1
155   REM BEGINN DES ZWEITEN EINZELSCHRITTS
160   IF M1=M GOTO 240
170   LET T=D(M,M1)
180   FOR N=1 TO N1
210     LET E(M,N)=E(M,N)-E(M1,N)*T
220     LET D(M,N)=D(M,N)-D(M1,N)*T
230   NEXT N
240   LET M=M+1
250   IF M LE N1 GOTO 160
260 NEXT M1
265 REM ENDE DER INVERSION
270 PRINT
275 PRINT 'INVERSE MATRIX'
280 MAT PRINT E
281 RESTORE N1**2
282 MAT READ D
283 MAT LET P=ZER(N1,N1)
284 MAT P=D*E
285 PRINT
286 PRINT 'KONTROLLMATRIX'
287 MAT PRINT P
340 DATA 49.96, 12.23, 36.78, 29.43
342 DATA 22.46, 38.72, 40.15, 11.29
344 DATA 36.45, 19.20, 37.28, 6.43
346 DATA 17.84, 40.02, 27.86, 39.27
350 STOP
360 PRINT 'EINGABEMATRIX IST SINGULAER'
400 END