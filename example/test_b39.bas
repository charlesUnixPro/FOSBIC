*  The accuracy of the control unity matrix P differs from book
  5 REM MATRIZENINVERISON
 10 DIM D(10,10),E(10,10),P(10,10)
 12 READ N1
 14 DATA 4
 16 PRINT 'EINGABEMATRIX'
 18 FOR I=1 TO N1
 20   FOR J=1 TO N1
 22     READ D(I,J)
 24     PRINT D(I,J),
 26   NEXT J
 28   PRINT
 30 NEXT I
 31 REM BILDEN EINER EINHEITSMATRIX
 32 FOR I=1 TO N1
 34   FOR J=1 TO N1
 36     LET E(I,J)=0
 38     IF I<>J GOTO 42
 40     LET E(I,J)=1
 42   NEXT J
 44 NEXT I
 46 REM BEGINN DER INVERSION
 80 FOR M1=1 TO N1
 90   LET T=D(M1,M1)
100   IF T=0 GOTO 360
105   REM BEGINN DES ERSTEN EINZELSCHRITTS
110   FOR N=1 TO N1
120     LET E(M1,N)=E(M1,N)/T
130     LET D(M1,N)=D(M1,N)/T
140   NEXT N
150   LET M=1
155   REM BEGINN DES ZWEITEN EINZELSCHRITTS
160   IF M1=M GOTO 240
170   LET T=D(M,M1)
180   FOR N=1 TO N1
210     LET E(M,N)=E(M,N)-E(M1,N)*T
220     LET D(M,N)=D(M,N)-D(M1,N)*T
230   NEXT N
240   LET M=M+1
250   IF M LE N1 GOTO 160
260 NEXT M1
265 REM ENDE DER INVERSION
270 PRINT
275 PRINT 'INVERSE MATRIX'
280 FOR I=1 TO N1
282   FOR J=1 TO N1
284     PRINT E(I,J),
286   NEXT J
288   PRINT
290 NEXT I
292 RESTORE N1**2
294 FOR I=1 TO N1
296   FOR J=1 TO N1
298     READ D(I,J)
300   NEXT J
302 NEXT I
304 FOR I=1 TO N1
306   FOR J=1 TO N1
308     LET P(I,J) = 0
310     FOR K=1 TO N1
312       LET P(I,J)=P(I,J)+D(I,K)*E(K,J)
314     NEXT K
316   NEXT J
318 NEXT I
319 PRINT
320 PRINT 'KONTROLLMATRIX'
322 FOR I=1 TO N1
324   FOR J=1 TO N1
326     PRINT P(I,J),
328   NEXT J
330   PRINT
332 NEXT I
340 DATA 49.96, 12.23, 36.78, 29.43
342 DATA 22.46, 38.72, 40.15, 11.29
344 DATA 36.45, 19.20, 37.28, 6.43
346 DATA 17.84, 40.02, 27.86, 39.27
350 STOP
360 PRINT 'EINGABEMATRIX IST SINGULAER'
400 END