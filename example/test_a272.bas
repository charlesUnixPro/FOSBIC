5    REM LINEARE REGRESSION UND KORRELATION
10   REM ***********************************************************************
15   REM ***** WENN DIE EINGELESENENEN DATA NICHT AUSGEDRUCKT WERDEN SOLLEN IST
16   REM ***** FUER A1 EINE 0 (NULL) EINZUGEBEN,SONST EINE ZAHL UNGLEICH NULL
20   REM ***********************************************************************
30   READ A1
40   REM ***********************************************************************
100  PRINT' LINEARE REGRESSIONS- UND KORRELATIONSANALYSE'
105  REM ***** DIE DIMENSIONIERUNG IST ABHAENGIG VON DER DATENMENGE *****
110  DIM V(100),W(100)
120  REM ***********************************************************************
130  PRINT
140  REM ***** ANZAHL DER ZAHLENPAARE WERDEN EIGELESEN *****
142  REM ***** N9 = GRENZE DER DIMENSIONIERUNG *****
145  LET N9=100
150  READ N
155  IF N <= N9 GOTO 160
157  PRINT 'DIM ERWEITERN, DA V UND W NUR BIS',N9,' DIMENSIONIERT'
158  GOTO 9999
160  IF N GE 3 THEN 200
170  PRINT' ----- FEHLER BEI EINGABE VON N, ES MUESSEN MEHR',
171  PRINT'ALS ZWEI PAARE EINGEGEBEN WERDEN'
180  PRINT
190  GOTO 9999
200  REM ***** DIE X UND Y WERTE WERDEN REIHENWEISE EINGELESEN *****
203  REM X WIRD IN V EINGELESEN
206  REM Y WIRD IN W EINGELESEN
210  FOR I=1 TO N
220    READ V(I)
230  NEXT I
231  FOR I=1 TO N
232    READ W(I)
234  NEXT I
240  REM ***** SUMMENBILDUNG *****
250  FOR I=1 TO N
260    LET X=X+V(I)
270    LET Y=Y+W(I)
280    LET X2=X2+V(I)**2
290    LET Y2=Y2+W(I)**2
300    LET Z=Z+V(I)*W(I)
310  NEXT I
350  REM **** BERECHNUNG DER VARIANZEN *****
355  LET X3=(N*X2-X**2)/(N*(N-1))
360  LET Y3=(N*Y2-Y**2)/(N*(N-1))
365  LET K3=(N*Z-X*Y)/(N*(N-1))
370  REM ***** BERECHNUNG STEIGUNG UND SCHNITTPUNKT MIT Y-ACHSE *****
375  LET B=K3/X3
380  LET A=(Y-B*X)/N
385  REM ***** BERECHNUNG DES BESTIMMTHEITSMASSES *****
390  LET B1=K3**2/(X3*Y3)
400  REM *****BERECHNUNG KORRELATIONSKOEFFIZIENT *****
410  LET R=K3/(SQR(X3)*SQR(Y3))
420  REM ***** BERECHNUNG DES STANDARDFEHLERS *****
430  LET S1=SQR(Y3*(1-B1))
440  PRINT
450  PRINT
455  REM ********* AUSDRUCK DER ERGEBNISSE *********
460  PRINT' ---- ERGEBNISSE DER LINEAREN REGRESSIONS-';
470  PRINT' UND KORRELATIONSANALYSE'
480  PRINT
485  PRINT
487  IF A1=0 THEN  536
490  PRINT'  X-VARIABLE','  Y-VARIABLE','  X**2 WERTE','  Y**2 WERTE',
491  PRINT'   X*Y WERTE'
496  PRINT
500  FOR I=1 TO N
511    PRINT V(I),W(I),V(I)**2,W(I)**2,V(I)*W(I)
520  NEXT I
535  PAGE
536  PRINT
539  PRINT'+--------------------------------------------+'
540  PRINT'I                                            I'
541  PRINT'I  ANZAHL DER WERTEPAARE.....',N,'I'
550  PRINT'I                                            I'
560  PRINT'I  SUMME DER X-WERTE.........',X,'I'
570  PRINT'I  SUMME DER Y-WERTE.........',Y,'I'
580  PRINT'I  SUMME VON X*X.............',Z,'I'
590  PRINT'I  SUMME DER X-QUADRATE......',X2,'I'
600  PRINT'I  SUMME DER Y-QUADRATE......',Y2,'I'
610  PRINT'I  MITTELWERT VON X..........',X/N,'I'
620  PRINT'I  MITTELWERT VON Y..........',Y/N,'I'
622  PRINT'I  VARIANZ VON X.............',X3,'I'
623  PRINT'I  VARIANZ VON Y.............',Y3,'I'
624  PRINT'I  KOVARIANZ.................',K3,'I'
630  PRINT'I  STEIGUNG B DER GERADEN....',B,'I'
640  PRINT'I  SCHNITTP. MIT DER Y-ACHSE.',A,'I'
650  PRINT'I                                            I'
660  PRINT'I  STANDARDFEHLER............',S1,'I'
670  PRINT'I                                            I'
680  PRINT'I  KORRELATIONSKOEFFIZIENT...',R,'I'
681  PRINT'I                                            I'
682  PRINT'I  BESTIMMTHEITSMASS.........',B1,'I'
683  PRINT'I                                            I'
684  PRINT'+--------------------------------------------+'
999  DATA 0
1000 DATA 20
1010 DATA 167,178,158,181,174,195,168,173,178,160
1011 DATA 169,188,153,176,177,161,193,201,175,156
1020 DATA 60,68,70,92,88,104,68,73,79,62
1021 DATA 72,89,53,78,79,67,110,121,92,63
9999 END