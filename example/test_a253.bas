*8
1   COMMENT I = STAREREIGNIS
2   COMMENT J = ENDEREIGNIS
3   COMMENT D = ZEITDAUER DER AKTIVITAET VON I NACH J
4   COMMENT T = FRUEHESTER START DER AKTIVITAET VON I NACH J
5   COMMENT A2 = SPAETESTER START DER AKTIVITAET VON I NACH J
6   COMMENT A3 = FRUHESTES ENDE DER AKTIVITAET VON I NACH J
7   COMMENT R = SPAETESTE ENDE DER AKTTIVITAET VON I NACH J
8   COMMENT A4 = TOTALE PUFFERZEIT
9   COMMENT A5 = FREIE PUFFERZEIT
10  COMMENT A6 = UNABHAENGIGE PUFFERZEIT
11  COMMENT A7 = BEDINGTE PUFFERZEIT
12  DIM I(50),J(50),D(50),A(50),T(50),R(50)
20  REM I9=MAXIMALE AKTIVITAETSZAHL = DIM-GRENZE
30  LET I9=50
40  LET K=0
50  LET K=K+1
60  READ I(K),J(K),D(K)
65  IF I$(K) = 'ENDE' GOTO 940
70  IF I(K) < 0 THEN 100
80  IF K >= I9 THEN 120
90  GOTO 50
100 LET N=K-1
110 GOTO 140
120 PRINT 'UEBERSPEICHERUNG',I9,'AKTIVITAETEN ERREICHT'
130 STOP
140 REM KLASSIFIZIERUNG DER AKTIVITAETEN
200 FOR K=1 TO N
210   LET A(K)=K
220 NEXT K
230 FOR K2=1 TO N
240   LET K3=0
250   FOR K=1 TO N-K2
260     LET A1=A(K)
270     LET A2=A(K+1)
280     IF I(A1)<I(A2) GOTO 340
290     IF I(A1)>I(A2) GOTO 310
300     IF J(A1)<J(A2) GOTO 340
310     LET K3=K3+1
320     LET A(K)=A2
330     LET A(K+1)=A1
340   NEXT K
350   IF K3=0 GOTO 380
360 NEXT K2
370 REM INITIALISIERUNG
380 FOR K=1 TO N
390   LET T(K)=0
400   LET R(K)=0
410 NEXT K
420 REM VORWAERTSSPEICHERUNG
430 FOR K=1 TO N
440   LET I1=I(A(K))
450   LET J1=J(A(K))
460   LET M=T(I1)+D(A(K))
470   IF T(J1)=0 THEN 490
480   IF T(J1)>=M GOTO 500
490   LET T(J1)=M
500 NEXT K
510 REM RUECKWAERTSRECHNUNG
520 LET R(J(A(N)))=T(J(A(N)))
530 FOR K=N TO 1 STEP -1
540   LET I1=I(A(K))
550   LET M=R(J(A(K)))-D(A(K))
560   IF R(I1)=0 GOTO 580
570   IF R(I1)<=M GOTO 590
580   LET R(I1)=M
590 NEXT K
600 REM ERGEBNISSE
610 PRINT
620 PRINT ' AKTIVITAET     START',
630 PRINT '    ENDE             PUFFERZEIT'
640 PRINT ' I  J DAUER  FRUEH  SPAET',
650 PRINT 'FRUEH SPAET   TOTAL  FREI  UNABH.  BEDINGT'
660 PRINT
670 FOR K=1 TO N
680   LET A1=A(K)
690   LET I1=I(A1)
700   LET J1=J(A1)
701   LET A2=R(J1)-D(A1)
702   LET A3=T(I1)+D(A1)
703   LET A4=R(J1)-T(I1)-D(A1)
704   LET A5=T(J1)-T(I1)-D(A1)
705   LET A6=T(J1)-R(I1)-D(A1)
706   IF A6 >= 0 GOTO 708
707   LET A6=0
708   LET A7=A4-A5
710   PRINT TAB(1);I1;TAB(5);J1;TAB(9);D(A1);
720   PRINT TAB(15);T(I1);TAB(22);A2;TAB(32);A3;TAB(39);R(J1);
730   PRINT TAB(45);A4;TAB(52);A5;TAB(59);A6;TAB(66);A7;' ';
770   IF R(J1)-T(I1)<>D(A1) GOTO 800
780   PRINT 'KRITISCH'
790   GOTO 810
800   PRINT
810 NEXT K
812 PRINT
815 PRINT 'PROJEKTDAUER =',R(J(A(N)))
820 DATA 6,8,0,6,9,8,7,8,40,8,9,32,9,10,8,10,11,8,10,12,24,11,12,0,11,13,6
830 DATA 1,2,144,1,4,360,2,3,8,3,4,0,3,5,12,3,6,225,3,7,200,4,5,8,5,6,35
840 DATA 12,13,4,13,14,4,-1,-1,-1
845 DATA 'ENDE',-1,-1
850 FOR K=1 TO I9
860   LET I(K)=0
870   LET J(K)=0
880   LET D(K)=0
890   LET A(K)=0
900   LET T(K)=0
910   LET R(K)=0
920 NEXT K
930 GOTO 40
940 STOP
950 END